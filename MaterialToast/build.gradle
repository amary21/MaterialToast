plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

def getArtifactId = { ->
    return "MaterialToast"
}

final VERSION_MAJOR = 0
final VERSION_MINOR = 0
final VERSION_PATCH = 1

android {
    namespace 'com.amary.materialtoast'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode getVersionCode(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)
        versionName getVersionName(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'com.amary.materialtoast' // Replace with group ID
            artifactId getArtifactId()
            version getVersionName(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)
            artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/amary21/MaterialToast")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}

private static int getVersionCode(int major, int minor, int patch){
    return major * 100000 + minor * 10000  + patch
}

private static String getVersionName(int major, int minor, int patch){
    return "${major}.${minor}.${patch}"
}